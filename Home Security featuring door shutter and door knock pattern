/* Detects patterns of knocks and triggers a motor to unlock
   it if the pattern is correct.
   
   By Steve Hoefer http://grathio.com
   Version 0.1.10.20.10
   Licensed under Creative Commons Attribution-Noncommercial-Share Alike 3.0
   http://creativecommons.org/licenses/by-nc-sa/3.0/us/
   (In short: Do what you want, just be sure to include this line and the four above it, and don't sell it or use it in anything you sell without contacting me.)
   
   Analog Pin 0: Piezo speaker (connected to ground with 1M pulldown resistor)
   Digital Pin 2: Switch to enter a new code.  Short this to enter programming mode.
   Digital Pin 3: DC gear reduction motor attached to the lock. (Or a motor controller or a solenoid or other unlocking mechanisim.)
   Digital Pin 4: Red LED. 
   Digital Pin 5: Green LED. 
   
   Update: Nov 09 09: Fixed red/green LED error in the comments. Code is unchanged. 
   Update: Nov 20 09: Updated handling of programming button to make it more intuitive, give better feedback.
   Update: Jan 20 10: Removed the "pinMode(knockSensor, OUTPUT);" line since it makes no sense and doesn't do anything.
 */
 
// Pin definitions
#define LEDR        4       // Status LED
#define LEDV        5             // Status LED
#define BUZZER      6

const int knockSensor = 0;          // Piezo sensor on pin 0.
const int programSwitch = 2;        // If this is high we program a new code.
const int lockMotor = 3;            // Gear motor used to turn the lock.
 
// Tuning constants.  Could be made vars and hoooked to potentiometers for soft configuration, etc.
const int threshold = 500;            // Minimum signal from the piezo to register as a knock
const int rejectValue = 25;         // If an individual knock is off by this percentage of a knock we don't unlock..
const int averageRejectValue = 15;  // If the average timing of the knocks is off by this percent we don't unlock.
const int knockFadeTime = 150;      // milliseconds we allow a knock to fade before we listen for another one. (Debounce timer.)
const int lockTurnTime = 650;       // milliseconds that we run the motor to get it to go a half turn.

const int maximumKnocks = 20;       // Maximum number of knocks to listen for.
const int knockComplete = 1200;     // Longest time to wait for a knock before we assume that it's finished.


// Variables.
int secretCode[maximumKnocks] = {50, 25, 25, 50, 100, 50, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0};  // Initial setup: "Shave and a Hair Cut, two bits."
int knockReadings[maximumKnocks];   // When someone knocks this array fills with delays between knocks.
int knockSensorValue = 0;           // Last reading of the knock sensor.
int programButtonPressed = false;   // Flag so we remember the programming button setting at the end of the cycle.

//GSM MODULE

#include <SoftwareSerial.h>
SoftwareSerial sim(10, 11);
int _timeout;
String _buffer;
String number = "+639351002945";

//ULTRASONIC SENSOR

#define trigPin 12
#define echoPin 13
int Buzzer = 8;

bool isOccupied = false;
bool isUnlocked = false;

void setup(){
    pinMode(lockMotor, OUTPUT);
    pinMode(LEDR, OUTPUT);
    pinMode(LEDV, OUTPUT);
    pinMode(programSwitch, INPUT);

    pinMode(trigPin, OUTPUT);
    pinMode(echoPin, INPUT);
    pinMode(Buzzer, OUTPUT);

    delay(7000); //delay for 7 seconds to make sure the modules get the signal
    _buffer.reserve(50);
    sim.begin(9600);
    delay(1000);
    
    Serial.begin(9600);                     // Uncomment the Serial.bla lines for debugging.
    Serial.println("Program started.");       // but feel free to comment them out after it's working right.
    digitalWrite(LEDV, HIGH);         // Green LED on, everything is go.

}

void loop() {
    // Listen for any knock at all.
    
    if(isOccupied == true && isUnlocked == true){
      Serial.println("A family member unlocked the house. Turning off the sensors...");
    }
    
    else if(isOccupied == false && isUnlocked == false){
      knockSensorValue = analogRead(knockSensor);
  
      if(digitalRead(programSwitch)==HIGH){   // is the program button pressed?
        programButtonPressed = true;          // Yes, so lets save that state
        digitalWrite(LEDR, HIGH);             // and turn on the red light too so we know we're programming.
      } 
      else {
        programButtonPressed = false;
        digitalWrite(LEDR, LOW);
      }
    
      if(knockSensorValue >= threshold){
        listenToSecretKnock();
      }

      int duration, distance;
      digitalWrite(trigPin, HIGH);
      delayMicroseconds(1000);
      digitalWrite(trigPin, LOW);
      duration = pulseIn(echoPin, HIGH);
      distance = (duration/2) / 29.1;
      
      if (distance <= 20 && distance >= 0){
        Serial.println("Object detected! Sending message to the owner!!!");
        
        tone(Buzzer, 400); // play 400 Hz tone for 500 ms
        delay(500);
        noTone(Buzzer);
        tone(Buzzer, 400); // play 400 Hz tone for 500 ms
        delay(500);
        noTone(Buzzer);
        tone(Buzzer, 400); // play 400 Hz tone for 500 ms
        delay(500);
        noTone(Buzzer);

        ultra_SendMessage();
      }
      delay(300);
    }
    
} 
//*************************************************************************************
//THIS IS FOR THE GSM MODULE

void piezo_SendMessage()
{
  //Serial.println ("Sending Message");
  sim.println("AT+CMGF=1");    //Sets the GSM Module in Text Mode
  delay(1000);
  //Serial.println ("Set SMS Number");
  sim.println("AT+CMGS=\"" + number + "\"\r"); //Mobile phone number to send message
  delay(1000);
  String SMS = "FRONT DOOR SUCCESSFULLY OPENED WITH THE SECRET KNOCK SYSTEM.";
  sim.println(SMS);
  delay(100);
  sim.println((char)26);// ASCII code of CTRL+Z
  delay(1000);
  _buffer = _readSerial();
}
void ultra_SendMessage()
{
  //Serial.println ("Sending Message");
  sim.println("AT+CMGF=1");    //Sets the GSM Module in Text Mode
  delay(1000);
  //Serial.println ("Set SMS Number");
  sim.println("AT+CMGS=\"" + number + "\"\r"); //Mobile phone number to send message
  delay(1000);
  String SMS = "SOME INTRUDER ENTERED THE HOUSE!";
  sim.println(SMS);
  delay(100);
  sim.println((char)26);// ASCII code of CTRL+Z
  delay(1000);
  _buffer = _readSerial();
}

String _readSerial() {
  _timeout = 0;
  while  (!sim.available() && _timeout < 12000  )
  {
    delay(13);
    _timeout++;
  }
  if (sim.available()) {
    return sim.readString();
  }
}


//THIS IS FOR THE GSM MODULE
//*************************************************************************************


// Records the timing of knocks.
void listenToSecretKnock(){
  Serial.println("knock starting");   

int i = 0;
  // First lets reset the listening array.
    for(i=0; i<maximumKnocks; i++){
      knockReadings[i]=0;
    }
  
int currentKnockNumber=0;               // Incrementer for the array.
int startTime=millis();                 // Reference for when this knock started.
int now=millis();
  
  digitalWrite(LEDV, LOW);            // we blink the LED for a bit as a visual indicator of the knock.
    if(programButtonPressed==true){
      digitalWrite(LEDR, LOW);            // and the red one too if we're programming a new knock.
    }
    delay(knockFadeTime);         // wait for this peak to fade before we listen to the next one.
    digitalWrite(LEDV, HIGH);  
    if(programButtonPressed==true){
      digitalWrite(LEDR, HIGH);                        
    }
    do {
      //listen for the next knock or wait for it to timeout. 
      knockSensorValue = analogRead(knockSensor);
      if(knockSensorValue >= threshold){                  //got another knock...
          //record the delay time.
          Serial.println("knock.");
          now = millis();
          knockReadings[currentKnockNumber] = now-startTime;
          currentKnockNumber++;             //increment the counter
          startTime = now;          
          // and reset our timer for the next knock
          digitalWrite(LEDV, LOW);  
          if(programButtonPressed == true){
            digitalWrite(LEDR, LOW);          // and the red one too if we're programming a new knock.
          }
          delay(knockFadeTime);             // again, a little delay to let the knock decay.
          digitalWrite(LEDV, HIGH);
          if(programButtonPressed == true){
            digitalWrite(LEDR, HIGH);                         
          }
      }
      now = millis();
    //did we timeout or run out of knocks?
    } while ((now-startTime < knockComplete) && (currentKnockNumber < maximumKnocks));
  
    //we've got our knock recorded, lets see if it's valid
    if(programButtonPressed==false){            // only if we're not in progrmaing mode.
      if(validateKnock() == true){
          triggerDoorUnlock(); 
      } 
      else {
          Serial.println("Secret knock failed.");
          tone(BUZZER, 0); // play 800Hz tone for 500ms
          delay(300);
          noTone(BUZZER);
          digitalWrite(BUZZER, LOW);
          digitalWrite(LEDV, LOW);      // We didn't unlock, so blink the red LED as visual feedback.
          for (i=0; i<4; i++){          
            digitalWrite(LEDR, HIGH);
            delay(100);
            digitalWrite(LEDR, LOW);
            delay(100);
          }
          digitalWrite(LEDV, HIGH);
      }
  } 
  else { // if we're in programming mode we still validate the lock, we just don't do anything with the lock
      validateKnock();
      // and we blink the green and red alternately to show that program is complete.
      Serial.println("New lock stored.");
      digitalWrite(LEDR, LOW);
      digitalWrite(LEDV, HIGH);
      for (i=0; i<3; i++){
        delay(100);
        digitalWrite(LEDR, HIGH);
        digitalWrite(LEDV, LOW);
        delay(100);
        digitalWrite(LEDR, LOW);
        digitalWrite(LEDV, HIGH);      
      }
  }
}

// Runs the motor (or whatever) to unlock the door.
void triggerDoorUnlock(){

  Serial.println("Door unlocked!");
  int i=0;

  // turn on the buzzer
  tone(BUZZER, 800); // play 800Hz tone for 500ms
  delay(500);
  noTone(BUZZER);
  delay(300);
  tone(BUZZER, 800); // play 800Hz tone for 500ms
  delay(1000);
  noTone(BUZZER);
  
  // turn the motor on for a bit.
  digitalWrite(lockMotor, HIGH);
  digitalWrite(LEDV, HIGH);            // And the green LED too.
  
  delay (lockTurnTime);                    // Wait a bit.
  
  digitalWrite(lockMotor, LOW);            // Turn the motor off.
  
  // Blink the green LED a few times for more visual feedback.
  for (i=0; i < 5; i++){   
      digitalWrite(LEDV, LOW);
      delay(100);
      digitalWrite(LEDV, HIGH);
      delay(100);
  }

  isOccupied = true;
  isUnlocked = true;

  piezo_SendMessage();
}

// Sees if our knock matches the secret.
// returns true if it's a good knock, false if it's not.
// todo: break it into smaller functions for readability.
boolean validateKnock(){
int i=0;
 
  // simplest check first: Did we get the right number of knocks?
int currentKnockCount = 0;
int secretKnockCount = 0;
int maxKnockInterval = 0;               // We use this later to normalize the times.
  
  for (i=0;i<maximumKnocks;i++){
      if (knockReadings[i] > 0){
          currentKnockCount++;
      }
      if (secretCode[i] > 0){           //todo: precalculate this.
          secretKnockCount++;
      }
    
      if (knockReadings[i] > maxKnockInterval){   // collect normalization data while we're looping.
          maxKnockInterval = knockReadings[i];
      }
    }
  
  // If we're recording a new knock, save the info and get out of here.
    if (programButtonPressed==true){
      for (i=0;i<maximumKnocks;i++){ // normalize the times
          secretCode[i]= map(knockReadings[i],0, maxKnockInterval, 0, 100); 
        }
      // And flash the lights in the recorded pattern to let us know it's been programmed.
        digitalWrite(LEDV, LOW);
        digitalWrite(LEDR, LOW);
        delay(1000);
        digitalWrite(LEDV, HIGH);
        digitalWrite(LEDR, HIGH);
        delay(50);
        for (i = 0; i < maximumKnocks ; i++){
          digitalWrite(LEDV, LOW);
          digitalWrite(LEDR, LOW);  
        // only turn it on if there's a delay
          if (secretCode[i] > 0){                                   
              delay( map(secretCode[i],0, 100, 0, maxKnockInterval)); // Expand the time back out to what it was.  Roughly. 
              digitalWrite(LEDV, HIGH);
              digitalWrite(LEDR, HIGH);
          }
          delay(50);
        }
      return false;   // We don't unlock the door when we are recording a new knock.
    }
  
    if (currentKnockCount != secretKnockCount){
      return false; 
  }
  
  /*  Now we compare the relative intervals of our knocks, not the absolute time between them.
      (ie: if you do the same pattern slow or fast it should still open the door.)
      This makes it less picky, which while making it less secure can also make it
      less of a pain to use if you're tempo is a little slow or fast. 
  */
int totaltimeDifferences=0;
int timeDiff=0;
    for (i=0;i<maximumKnocks;i++){ // Normalize the times
      knockReadings[i]= map(knockReadings[i],0, maxKnockInterval, 0, 100);      
      timeDiff = abs(knockReadings[i]-secretCode[i]);
      if (timeDiff > rejectValue){ // Individual value too far out of whack
          return false;
      }
      totaltimeDifferences += timeDiff;
    }
    // It can also fail if the whole thing is too inaccurate.
    if (totaltimeDifferences/secretKnockCount>averageRejectValue){
      return false; 
    }
  return true;
} 
